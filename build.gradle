buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.7.9"
    }
}

group = 'my.wiki'
version = '1.0-SNAPSHOT'

subprojects {
    apply plugin: 'java'
    apply plugin: "org.springframework.boot"
    apply plugin: 'io.spring.dependency-management'

    group rootProject.group
    version rootProject.version

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation('org.springframework.boot:spring-boot-starter-web')

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    }

    compileJava {
        sourceCompatibility = 11
        targetCompatibility = 11
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        sourceCompatibility = 11
        targetCompatibility = 11
        options.encoding = 'UTF-8'
    }

    test {
        useJUnitPlatform()
    }
}

configure(project(":crawler")) {
    dependencies {
        implementation('org.springframework.kafka:spring-kafka')
        implementation(project(":common"))
    }
}

configure(project(":graph")) {
    //TODO убрать после создания main-класса
    bootJar {
        enabled = false
    }
}

configure(project(":unifier")) {
    //TODO убрать после создания main-класса
    bootJar {
        enabled = false
    }
}

configure(project(":common")) {
    dependencies {
        implementation('org.springframework.kafka:spring-kafka')
    }
    bootJar {
        enabled = false
    }
}